#version 450
#include "Common.glslh"

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable

layout(location = 0) in vec2 outTexCoord;

layout(set = 0, binding = 0) uniform UniformBuffer
{
	float BloomIntensity;
	int ToneMapIndex;
	float Saturation;
	float Contrast;
	float Brightness;
	float TargetLuminance;    // middle-grey target (typically 0.18)
	float UseAdaptive;        // 0 or 1; non-bool to keep std140 alignment
	float _pad;               // keep UBO 16-byte aligned for std140
} ubo;

layout(set = 0, binding = 1) uniform sampler2D u_Texture;
layout(set = 0, binding = 2) uniform sampler2D u_BloomTexture;

layout(std430, set = 0, binding = 3) readonly buffer AdaptiveLuminance
{
    float AverageLuminance;
} adapt;

layout(location = 0) out vec4 outFrag;

vec3 ACESApprox(vec3 v)
{
    v *= 0.6f;
    float a = 2.51f;
    float b = 0.03f;
    float c = 2.43f;
    float d = 0.59f;
    float e = 0.14f;
    return clamp((v*(a*v+b))/(v*(c*v+d)+e), 0.0f, 1.0f);
}

// Based on http://www.oscars.org/science-technology/sci-tech-projects/aces
vec3 ACESTonemap(vec3 color)
{
	//
	//color = pow(abs(color), vec3(0.75f));
	//color *= 1.07f;
	//
	
	mat3 m1 = mat3(
				   0.59719, 0.07600, 0.02840,
				   0.35458, 0.90834, 0.13383,
				   0.04823, 0.01566, 0.83777
				   );
	mat3 m2 = mat3(
				   1.60475, -0.10208, -0.00327,
				   -0.53108, 1.10813, -0.07276,
				   -0.07367, -0.00605, 1.07602
				   );
	vec3 v = m1 * color;
	vec3 a = v * (v + 0.0245786) - 0.000090537;
	vec3 b = v * (0.983729 * v + 0.4329510) + 0.238081;
	return clamp(m2 * (a / b), 0.0, 1.0);
}

vec3 linearToneMapping(vec3 color)
{
	color = clamp(color, 0., 1.);
	return color;
}

vec3 simpleReinhardToneMapping(vec3 color)
{
	float exposure = 1.5;
	color *= exposure/(1. + color / exposure);
	return color;
}

vec3 lumaBasedReinhardToneMapping(vec3 color)
{
	float luma = dot(color, vec3(0.2126, 0.7152, 0.0722));
	float toneMappedLuma = luma / (1. + luma);
	color *= toneMappedLuma / luma;
	return color;
}

vec3 whitePreservingLumaBasedReinhardToneMapping(vec3 color)
{
	float white = 2.;
	float luma = dot(color, vec3(0.2126, 0.7152, 0.0722));
	float toneMappedLuma = luma * (1. + luma / (white*white)) / (1. + luma);
	color *= toneMappedLuma / luma;
	return color;
}

vec3 filmicToneMapping(vec3 color)
{
	color = max(vec3(0.), color - vec3(0.004));
	color = (color * (6.2 * color + .5)) / (color * (6.2 * color + 1.7) + 0.06);
	return color;
}

vec3 Uncharted2ToneMapping(vec3 colour)
{
	float A = 0.15;
    float B = 0.50;
    float C = 0.10;
    float D = 0.20;
    float E = 0.02;
    float F = 0.30;
    float W = 11.2;
	return ((colour * (A * colour + C * B) + D * E) / (colour * (A * colour + B) + D * F)) - E / F;
}

vec3 uncharted2_tonemap_partial(vec3 x)
{
    float A = 0.15f;
    float B = 0.50f;
    float C = 0.10f;
    float D = 0.20f;
    float E = 0.02f;
    float F = 0.30f;
    return ((x*(A*x+C*B)+D*E)/(x*(A*x+B)+D*F))-E/F;
}

vec3 uncharted2_filmic(vec3 v)
{
    float exposure_bias = 1.0f;
    vec3 curr = uncharted2_tonemap_partial(v * exposure_bias);
	
    vec3 W = vec3(11.2f);
    vec3 white_scale = vec3(1.0f) / uncharted2_tonemap_partial(W);
    return curr * white_scale;
}

float luminance(vec3 v)
{
    return dot(v, vec3(0.2126f, 0.7152f, 0.0722f));
}

vec3 change_luminance(vec3 c_in, float l_out)
{
    float l_in = luminance(c_in);
    return c_in * (l_out / l_in);
}

vec3 reinhard_extended(vec3 v, float max_white)
{
    vec3 numerator = v * (1.0f + (v / vec3(max_white * max_white)));
    return numerator / (1.0f + v);
}

vec3 reinhard(vec3 v)
{
    return v / (1.0f + v);
}

vec3 reinhard_jodie(vec3 v)
{
    float l = luminance(v);
    vec3 tv = v / (1.0f + v);
    return mix(v / (1.0f + l), tv, tv);
}

vec3 reinhard_extended_luminance(vec3 v, float max_white_l)
{
    float l_old = luminance(v);
    float numerator = l_old * (1.0f + (l_old / (max_white_l * max_white_l)));
    float l_new = numerator / (1.0f + l_old);
    return change_luminance(v, l_new);
}

// Hash for per-pixel white noise. iq / Hugo Elias style.
float hash12(vec2 p)
{
    vec3 p3 = fract(vec3(p.xyx) * 0.1031);
    p3 += dot(p3, p3.yzx + 33.33);
    return fract((p3.x + p3.y) * p3.z);
}

// Triangular PDF noise in [-1,1]. Two uniform samples summed.
// Better than uniform dither: hides quantization without raising noise floor as much.
vec3 screenSpaceDither(vec2 fragCoord)
{
    float n0 = hash12(fragCoord);
    float n1 = hash12(fragCoord + vec2(7.31, 13.17));
    vec3 tri = vec3((n0 + n1) - 1.0);
    return tri / 255.0;
}

void main()
{
	vec3 colour = texture(u_Texture, outTexCoord).rgb;

	vec3 bloom = texture(u_BloomTexture, outTexCoord).rgb * ubo.BloomIntensity;

	colour += bloom;

	// Exposure scale: adaptive path uses target/avg ratio (clamped to keep
	// pathological dark frames from blowing up). When adaptive is off we
	// leave the scene radiance untouched — the engine already bakes its
	// physical-camera exposure into forward lighting; the tonemap stage
	// shouldn't apply it a second time.
	if(ubo.UseAdaptive > 0.5)
	{
		float avgLum       = max(adapt.AverageLuminance, 1e-4);
		float exposureScale = clamp(ubo.TargetLuminance / avgLum, 0.05, 32.0);
		colour *= exposureScale;
	}

	int i = ubo.ToneMapIndex;
	if (i == 1) colour = linearToneMapping(colour);
	else if (i == 2) colour = reinhard_jodie(colour);
	else if (i == 3) colour = lumaBasedReinhardToneMapping(colour);
	else if (i == 4) colour = whitePreservingLumaBasedReinhardToneMapping(colour);
	else if (i == 5) colour = uncharted2_filmic(colour);
	else if (i == 6) colour = ACESTonemap(colour);
	
	colour = Gamma(colour);

	colour.rgb = (colour.rgb - 0.5f) * ubo.Contrast + 0.5f + ubo.Brightness;
	float lum = dot(colour.rgb, vec3(0.3086, 0.6094, 0.0820));
	colour.rgb = mix(vec3(lum), colour.rgb, ubo.Saturation);

	colour.rgb += screenSpaceDither(gl_FragCoord.xy);

	outFrag = vec4(colour, 1.0);
}